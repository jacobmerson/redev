## source:
## https://gitlab.com/jhamberg/cmake-examples/-/blob/386b8d36848d76c4ca451d3b0af338017ac844ad/cmake/CheckGit.cmake
#
#MIT License
#
#Copyright (c) 2901 Jonathan Hamberg
#
#Permission is hereby granted, free of charge, to any person obtaining a copy
#of this software and associated documentation files (the "Software"), to deal
#in the Software without restriction, including without limitation the rights
#to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
#copies of the Software, and to permit persons to whom the Software is
#furnished to do so, subject to the following conditions:
#
#The above copyright notice and this permission notice shall be included in all
#copies or substantial portions of the Software.
#
#THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
#IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
#FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
#AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
#LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
#OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
#SOFTWARE.
#

set(CURRENT_LIST_DIR ${CMAKE_CURRENT_LIST_DIR})
if (NOT DEFINED pre_configure_dir)
    set(pre_configure_dir ${CMAKE_CURRENT_LIST_DIR})
endif ()

if (NOT DEFINED post_configure_dir)
    set(post_configure_dir ${CMAKE_BINARY_DIR}/generated)
endif ()

set(pre_configure_file ${pre_configure_dir}/redev_git_version.cpp.in)
set(post_configure_file ${post_configure_dir}/redev_git_version.cpp)

function(CheckGitWrite git_hash)
    file(WRITE ${CMAKE_BINARY_DIR}/git-state.txt ${git_hash})
endfunction()

function(CheckGitRead git_hash)
    if (EXISTS ${CMAKE_BINARY_DIR}/git-state.txt)
        file(STRINGS ${CMAKE_BINARY_DIR}/git-state.txt CONTENT)
        LIST(GET CONTENT 0 var)

        set(${git_hash} ${var} PARENT_SCOPE)
    endif ()
endfunction()

function(CheckGitVersion)
    # Get the latest abbreviated commit hash of the working branch
    execute_process(
        COMMAND git log -1 --format=%h
        WORKING_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}
        OUTPUT_VARIABLE GIT_HASH
        OUTPUT_STRIP_TRAILING_WHITESPACE
        )

    CheckGitRead(GIT_HASH_CACHE)
    if (NOT EXISTS ${post_configure_dir})
        file(MAKE_DIRECTORY ${post_configure_dir})
    endif ()

    if (NOT EXISTS ${post_configure_dir}/redev_git_version.h)
        file(COPY ${pre_configure_dir}/redev_git_version.h DESTINATION ${post_configure_dir})
    endif()

    if (NOT DEFINED GIT_HASH_CACHE)
        set(GIT_HASH_CACHE "INVALID")
    endif ()

    # Only update the git_version.cpp if the hash has changed. This will
    # prevent us from rebuilding the project more than we need to.
    if (NOT ${GIT_HASH} STREQUAL ${GIT_HASH_CACHE} OR NOT EXISTS ${post_configure_file})
        # Set che GIT_HASH_CACHE variable the next build won't have
        # to regenerate the source file.
        CheckGitWrite(${GIT_HASH})

        configure_file(${pre_configure_file} ${post_configure_file} @ONLY)
    endif ()

endfunction()

function(CheckInGitRepo ret)
    execute_process(
        COMMAND git rev-parse
        WORKING_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}
        RESULT_VARIABLE GIT_EXISTS_RESULT
        )
    if(GIT_EXISTS_RESULT EQUAL 0)
      set(${ret} true PARENT_SCOPE)
    else()
      set(${ret} false PARENT_SCOPE)
    endif()
endfunction()

function(CheckGitSetup)
    CheckInGitRepo(IN_GIT_REPO)
    if(NOT IN_GIT_REPO)
      message(STATUS "Not in Git Repo, version set to ${CMAKE_PROJECT_VERSION}")
      set(GIT_HASH ${CMAKE_PROJECT_VERSION})
    else()
      add_custom_target(AlwaysCheckGit COMMAND ${CMAKE_COMMAND}
          -DRUN_CHECK_GIT_VERSION=1
          -Dpre_configure_dir=${pre_configure_dir}
          -Dpost_configure_file=${post_configure_dir}
          -DGIT_HASH_CACHE=${GIT_HASH_CACHE}
          -P ${CURRENT_LIST_DIR}/CheckGit.cmake
          BYPRODUCTS ${post_configure_file}
          )
      endif()

      add_library(redev_git_version ${post_configure_file})
      target_include_directories(redev_git_version PUBLIC
        "$<BUILD_INTERFACE:${CMAKE_BINARY_DIR}/generated/>"
        "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/>")

      if(IN_GIT_REPO)
        add_dependencies(redev_git_version AlwaysCheckGit)
      else()
        if (NOT EXISTS ${post_configure_dir})
            file(MAKE_DIRECTORY ${post_configure_dir})
        endif ()

        if (NOT EXISTS ${post_configure_dir}/redev_git_version.h)
            file(COPY ${pre_configure_dir}/redev_git_version.h DESTINATION ${post_configure_dir})
        endif()
        configure_file(${pre_configure_file} ${post_configure_file} @ONLY)
      endif()

      set(tgt_name "redev_git_version")
      install(TARGETS ${tgt_name} EXPORT ${tgt_name}-target
          RUNTIME DESTINATION bin
          ARCHIVE DESTINATION lib
          LIBRARY DESTINATION lib)
      install(EXPORT ${tgt_name}-target NAMESPACE ${PROJECT_NAME}::
              DESTINATION lib/cmake/${PROJECT_NAME})
      set(${PROJECT_NAME}_EXPORTED_TARGETS
          ${${PROJECT_NAME}_EXPORTED_TARGETS} ${tgt_name} PARENT_SCOPE)
    if(IN_GIT_REPO)
      CheckGitVersion()
    endif() 
endfunction()

# This is used to run this function from an external cmake process.
if (RUN_CHECK_GIT_VERSION)
    CheckGitVersion()
endif ()
